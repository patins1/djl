group "ai.djl.dlr"

dependencies {
    api project(":api")

    testImplementation(project(":testing"))
    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
    testRuntimeOnly project(":engines:pytorch:pytorch-engine")
    testRuntimeOnly project(":engines:pytorch:pytorch-jni")
}

compileJava.dependsOn(processResources)

processResources {
    outputs.dir file("${project.buildDir}/classes/java/main/jnilib")
    doLast {
        def url = "https://publish.djl.ai/dlr-${dlr_version}/jnilib/${djl_version}"
        def files = [
                "osx-x86_64/libdjl_dlr.dylib": "osx-x86_64/libdjl_dlr.dylib",
                "linux-x86_64/libdjl_dlr.so" : "linux-x86_64/libdjl_dlr.so"
        ]
        def jnilibDir = "${project.projectDir}/jnilib/${djl_version}"
        files.each { entry ->
            def file = new File("${jnilibDir}/${entry.value}")
            if (file.exists()) {
                project.logger.lifecycle("prebuilt or cached file found for ${entry.value}")
            } else {
                def nativeDir = "${project.parent.projectDir}/dlr-native/jnilib/${djl_version}/"
                def jnilibFile = new File("${nativeDir}/${entry.value}")
                if (jnilibFile.exists()) {
                    project.logger.lifecycle("Copying " + jnilibFile)
                    copy {
                        from jnilibFile
                        into file.getParent()
                    }
                } else if (!project.hasProperty("jni")) {
                    project.logger.lifecycle("Downloading ${url}/${entry.key}")
                    file.getParentFile().mkdirs()
                    new URL("${url}/${entry.key}").withInputStream { i -> file.withOutputStream { it << i } }
                }
            }
        }
        copy {
            from jnilibDir
            into "${project.buildDir}/classes/java/main/jnilib"
        }

        // write properties
        def propFile = file("${project.buildDir}/classes/java/main/dlr-engine.properties")
        propFile.text = "djl_version=${version}\ndlr_version=${dlr_version}"
    }
}

test {
    environment "PATH", "src/test/bin:${environment.PATH}"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "dlr-engine"
            pom {
                name = "DJL Engine Adapter for DLR"
                description = "Deep Java Library (DJL) Engine Adapter for DLR"
                url = "http://www.djl.ai/engines/dlr/${project.name}"
            }
        }
    }
}

clean.doFirst {
    delete System.getProperty("user.home") + "/.djl.ai/dlr"
}
